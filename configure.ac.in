AC_INIT([gpicker], @@VERSION@@)
AC_CONFIG_HEADERS(config.h)
AM_INIT_AUTOMAKE
AC_CONFIG_FILES([Makefile lib/Makefile])
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_RANLIB
gl_EARLY

AC_ARG_ENABLE(timing, [AS_HELP_STRING(--enable-timing, Enable output of timing info on stderr)],
                      [AC_DEFINE([WITH_TIMING], 1, [Output timing info])])
AC_ARG_WITH([gtk], AS_HELP_STRING([--without-gtk], [Ignore presence of GTK+ libs]))

# check for 'check' unit-testing framework
PKG_CHECK_MODULES([check], [check], [HAVE_CHECK=yes], [
HAVE_CHECK=no
echo "Regression tests will not be built"
])
AM_CONDITIONAL([HAVE_CHECK], [test x$HAVE_CHECK = xyes])

AC_DEFINE([FIND_INVOCATION], ["find . '!' -wholename '*.git/*' -a '!' -wholename '*.hg/*']dnl
[ -a '!' -wholename '*.svn/*' -a '!' -wholename '*.bzr/*']dnl
[ -a '!' -wholename '*CVS/*' -type f -follow -print0"], [Command line to fetch list of files])

HAVE_GTK=no
AS_IF([test "x$with_gtk" != "xno"], [
   HAVE_GTK=yes
   PKG_CHECK_MODULES([gtk2], [gtk+-2.0],, [HAVE_GTK=no])
   PKG_CHECK_MODULES([gthread], [gthread-2.0],, [HAVE_GTK=no])
])
PKG_CHECK_MODULES([glib2], [glib-2.0 >= 2.14],, [echo "glib-2.0 >= 2.14 required." >&2; exit ])
AM_CONDITIONAL([HAVE_GTK], [test x$HAVE_GTK = xyes])

AC_SUBST(EXTRA_CPPFLAGS)

if test "x$HAVE_GTK" = "xyes"; then
uname_res=`uname -s`
if test "x$uname_res" = "xDarwin"; then
  EXTRA_CPPFLAGS=-xobjective-c
  gtk2_LIBS="$gtk2_LIBS -framework Cocoa"
fi

AC_MSG_CHECKING([whether -m32 is required to link gtk])
AC_LANG_CONFTEST([AC_LANG_CALL([],[gtk_main])])
ac_func_search_save_LIBS=$LIBS
ac_func_search_save_CFLAGS=$CFLAGS
LIBS="$LIBS $gtk2_LIBS $glib2_LIBS $gthread_LIBS"
AC_LINK_IFELSE([], [AC_MSG_RESULT([no])], [
  dnl link failed. Try -m32
  CFLAGS="-m32 $CFLAGS"
  AC_LINK_IFELSE([], [gtk2_CFLAGS="-m32 $gtk2_CFLAGS"],
     [AC_MSG_ERROR([linking with gtk2 failed! Something is wrong.])])
  AC_MSG_RESULT([yes])
])
LIBS=$ac_func_search_save_LIBS
CFLAGS=$ac_func_search_save_CFLAGS
fi # HAVE_GTK=1

ac_func_search_save_LIBS=$LIBS
ac_func_search_save_CFLAGS=$CFLAGS
ac_func_search_save_CPPLAGS=$CPPFLAGS
CFLAGS="$gtk2_CFLAGS $glib2_CFLAGS $gthread_CFLAGS $CFLAGS"
CPPFLAGS="$gtk2_CFLAGS $glib2_CFLAGS $gthread_CFLAGS $CPPFLAGS"
LIBS="$LIBS $gtk2_LIBS $glib2_LIBS $gthread_LIBS"
AC_CHECK_HEADER([gdk/gdkx.h], [AC_DEFINE([WITH_GDK_X11], 1, [GDK wraps X11])])
LIBS=$ac_func_search_save_LIBS
CFLAGS=$ac_func_search_save_CFLAGS
CPPFLAGS=$ac_func_search_save_CPPFLAGS

gl_INIT

AC_OUTPUT
